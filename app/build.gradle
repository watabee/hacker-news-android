apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.github.watabee.hackernews"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_BASE_URL", "\"https://hacker-news.firebaseio.com\""
    }

    signingConfigs {
        debug {
            storeFile file("${rootDir}/config/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    def secretFile = file("${rootDir}/secret/secret.gradle")
    if (secretFile.exists()) {
        apply from: secretFile.absolutePath, to: android
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            applicationIdSuffix = '.debug'
        }
        release {
            if (secretFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true

            def files = project.file("proguards")
                .listFiles()
                .findAll { it.name.startsWith('proguard') }
                .toList()
                .toArray() + getDefaultProguardFile("proguard-android-optimize.txt")

            proguardFiles(files)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled true
    }
}

dependencies {
    implementation project(":modules:base-android")
    implementation project(":modules:api")
    implementation project(":modules:db")
    implementation project(":modules:cache-deleter")
    implementation project(":modules:worker-manager")

    implementation deps.kotlin.stdlib
    implementation deps.androidx.appcompat
    implementation deps.androidx.activity
    implementation deps.androidx.fragment
    implementation deps.androidx.recyclerview
    implementation deps.androidx.core
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.browser
    implementation deps.androidx.cardview
    implementation deps.material

    implementation deps.androidx.lifecycle.extensions
    implementation deps.androidx.lifecycle.livedata
    implementation deps.androidx.lifecycle.viewmodel.ktx
    implementation deps.androidx.lifecycle.viewmodel.savedstate

    implementation deps.rxjava
    implementation deps.rxandroid
    implementation deps.rxkotlin

    implementation deps.dagger.core
    kapt deps.dagger.compiler
    implementation deps.dagger.android.support
    kapt deps.dagger.android.processor
    compileOnly deps.dagger.assistedInject.annotations
    kapt deps.dagger.assistedInject.processor

    implementation deps.firebase.core
    implementation deps.firebase.crashlytics

    implementation deps.groupie.core
    implementation deps.groupie.databinding

    testImplementation deps.junit
    androidTestImplementation deps.androidx.test.runner
    androidTestImplementation deps.androidx.test.espresso.core
}

// Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'