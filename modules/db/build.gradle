apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        minSdkVersion versions.minSdk

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                 "$projectDir/schemas".toString()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        test.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

configurations {
    [testImplementation, androidTestImplementation]*.extendsFrom sharedTestImplementation
}

dependencies {
    implementation project(":modules:base-android")

    implementation deps.kotlin.stdlib

    implementation deps.androidx.room.runtime
    kapt deps.androidx.room.compiler
    implementation deps.androidx.room.rxjava

    implementation deps.rxjava

    api deps.dagger.core
    kapt deps.dagger.compiler

    sharedTestImplementation deps.androidx.test.junit
    sharedTestImplementation deps.androidx.test.truth
    sharedTestImplementation deps.androidx.test.runner
    sharedTestImplementation deps.androidx.test.rules
    sharedTestImplementation deps.androidx.room.testing

    testImplementation deps.androidx.arch.testing
    testImplementation deps.robolectric
    testImplementation deps.androidx.lifecycle.livedata
}